MIT/GNU Scheme running under GNU/Linux
Type `^C' (control-C) followed by `H' to obtain information about interrupts.

Copyright (C) 2020 Massachusetts Institute of Technology
This is free software; see the source for copying conditions. There is NO
warranty; not even for MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

Image saved on Sunday March 7, 2021 at 3:24:56 PM
  Release 11.2 || SF || LIAR/x86-64

1 ]=> ; memo-proc procedure
(define (memo-proc proc)
  (let ((already-run? false) (result false))
    (lambda ()
      (if (not already-run?)
          (begin (set! result (proc))
                 (set! already-run? true)
                 result)
          result))))
;Value: memo-proc

1 ]=> #|
 | §3.5.1 Footnote 56:
 |
 | Although stream-car and stream-cdr can be defined as procedures, cons-stream
 | must be a special form. If cons-stream were a procedure, then, according to
 | our model of evaluation, evaluating (cons-stream <a> <b>) would automatically
 | cause <b> to be evaluated, which is precisely what we do not want to happen.
 | For the same reason, delay must be a special form, though force can be an
 | ordinary procedure.
 |#

; delay procedure -- create thunk
; (define (delay expr) (lambda () expr))
; (define (delay expr) (memo-proc (lambda () expr)))
(define-syntax delay
  (syntax-rules ()
    ((delay ?expr) (memo-proc (lambda () ?expr)))))
;Value: delay

1 ]=> ; force procedure -- evaluate thunk
(define (force delayed-expr) (delayed-expr))
;Value: force

1 ]=> ; the-empty-stream defn
(define the-empty-stream '())
;Value: the-empty-stream

1 ]=> ; cons-stream constructor procedure
; (define (cons-stream a b) (cons a (delay b)))
(define-syntax cons-stream
  (syntax-rules ()
    ((cons-stream a b) (cons a (delay b)))))
;Value: cons-stream

1 ]=> ; stream-car selector procedure
(define (stream-car stream) (car stream))
;Value: stream-car

1 ]=> ; stream-cdr selector procedure
(define (stream-cdr stream) (force (cdr stream)))
;Value: stream-cdr

1 ]=> ; stream-null? predicate procedure
(define (stream-null? stream) (null? stream))
;Value: stream-null?

1 ]=> ; stream-ref procedure
(define (stream-ref s n)
  (if (= n 0)
      (stream-car s)
      (stream-ref (stream-cdr s) (- n 1))))
;Value: stream-ref

1 ]=> #|
 | ; stream-map procedure
 | (define (stream-map proc s)
 |   (if (stream-null? s)
 |       the-empty-stream
 |       (cons-stream (proc (stream-car s))
 |                    (stream-map proc (stream-cdr s)))))
 |#

; stream-for-each procedure
(define (stream-for-each proc s)
  (if (stream-null? s)
      'done
      (begin (proc (stream-car s))
             (stream-for-each proc (stream-cdr s)))))
;Value: stream-for-each

1 ]=> #|
 | ; display-stream procedure
 | (define (display-stream s)
 |   (stream-for-each display-line s))
 |
 | ; display-line procedure
 | (define (display-line x)
 |   (newline)
 |   (display x))
 |#

; display-stream procedure (instructor's manual, augmented)
(define display-stream
  (let ()
    (define (iter stream delim)
      (cond ((stream-null? stream) (display "]"))
            (else (display delim)
                  (display (stream-car stream))
                  (iter (stream-cdr stream) " "))))
    (lambda (stream)
      (newline)
      (display "[")
      (iter stream ""))))
;Value: display-stream

1 ]=> ; stream-enumerate-interval procedure
(define (stream-enumerate-interval low high)
  (if (> low high)
      the-empty-stream
      (cons-stream
       low
       (stream-enumerate-interval (+ low 1) high))))
;Value: stream-enumerate-interval

1 ]=> ; stream-filter procedure
(define (stream-filter pred stream)
  (cond ((stream-null? stream) the-empty-stream)
        ((pred (stream-car stream))
         (cons-stream (stream-car stream)
                      (stream-filter pred
                                     (stream-cdr stream))))
        (else (stream-filter pred (stream-cdr stream)))))
;Value: stream-filter

1 ]=> ; stream-map procedure
(define (stream-map proc . argstreams)
  (if (stream-null? (car argstreams))
      the-empty-stream
      (cons-stream
        (apply proc (map stream-car argstreams))
        (apply stream-map
          (cons proc (map stream-cdr argstreams))))))
;Value: stream-map

1 ]=> ; stream-take procedure
(define (stream-take s n)
  (cond ((< n 0) (error "Subscript"))
        ((= n 0) '())
        ((stream-null? s) (error "Subscript"))
        (else (cons (stream-car s)
                    (stream-take (stream-cdr s) (- n 1))))))
;Value: stream-take

1 ]=> ; integers-starting-from procedure
(define (integers-starting-from n)
  (cons-stream n (integers-starting-from (+ n 1))))
;Value: integers-starting-from

1 ]=> ; divisible? predicate procedure
(define (divisible? x y) (= (remainder x y) 0))
;Value: divisible?

1 ]=> ; (Sieve of Eratosthenes) sieve procedure
(define (sieve stream)
  (cons-stream
   (stream-car stream)
   (sieve (stream-filter
           (lambda (x)
             (not (divisible? x (stream-car stream))))
           (stream-cdr stream)))))
;Value: sieve

1 ]=> ; (Sieve of Eratosthenes) primes stream
(define primes (sieve (integers-starting-from 2)))
;Value: primes

1 ]=> ; ones stream
(define ones (cons-stream 1 ones))
;Value: ones

1 ]=> ; add-streams procedure
(define (add-streams s1 s2)
  (stream-map + s1 s2))
;Value: add-streams

1 ]=> ; integers stream
(define integers (cons-stream 1 (add-streams ones integers)))
;Value: integers

1 ]=> ; fibs stream
(define fibs
  (cons-stream 0 (cons-stream 1 (add-streams (stream-cdr fibs) fibs))))
;Value: fibs

1 ]=> ; scale-stream procedure
(define (scale-stream stream factor)
  (stream-map (lambda (x) (* x factor)) stream))
;Value: scale-stream

1 ]=> ; mul-streams procedure
(define (mul-streams s1 s2)
  (stream-map * s1 s2))
;Value: mul-streams

1 ]=> ; partial-sums procedure
(define (partial-sums s)
  (add-streams s (cons-stream 0 (partial-sums s))))
;Value: partial-sums

1 ]=> ; merge procedure
(define (merge s1 s2)
  (cond ((stream-null? s1) s2)
        ((stream-null? s2) s1)
        (else
         (let ((s1car (stream-car s1))
               (s2car (stream-car s2)))
           (cond ((< s1car s2car)
                  (cons-stream s1car (merge (stream-cdr s1) s2)))
                 ((> s1car s2car)
                  (cons-stream s2car (merge s1 (stream-cdr s2))))
                 (else
                  (cons-stream s1car
                               (merge (stream-cdr s1)
                                      (stream-cdr s2)))))))))
;Value: merge

1 ]=> ; Hamming stream
(define S (cons-stream 1 (merge (scale-stream S 2)
                                (merge (scale-stream S 3)
                                       (scale-stream S 5)))))
;Value: s

1 ]=> ; expand procedure
(define (expand num den radix)
  (cons-stream
   (quotient (* num radix) den)
   (expand (remainder (* num radix) den) den radix)))
;Value: expand

1 ]=> ; harmonic-series stream
(define harmonic-series
  (stream-map / integers))
;Value: harmonic-series

1 ]=> ; integrate series procedure
(define (integrate-series as)
  (mul-streams harmonic-series as))
;Value: integrate-series

1 ]=> ; exp-series stream (e^x)
(define exp-series
  (cons-stream 1 (integrate-series exp-series)))
;Value: exp-series

1 ]=> ; cosine-series stream (cos x)
(define cosine-series
  (cons-stream 1 (integrate-series (scale-stream sine-series -1))))
;Value: cosine-series

1 ]=> ; sine-series stream (sin x)
(define sine-series
  (cons-stream 0 (integrate-series cosine-series)))
;Value: sine-series

1 ]=> #|
 | Exercise 3.60
 |
 | With power series represented as streams of coefficients as in exercise
 | 3.59, adding series is implemented by add-streams. Complete the definition
 | of the following procedure for multiplying series:
 |
 | (define (mul-series s1 s2)
 |   (cons-stream <??> (add-streams <??> <??>)))
 |
 | You can test your procedure by verifying that sin2 x + cos2 x = 1, using the
 | series from exercise 3.59.
 |
 | Answer
 |
 | (a₀ + a₁x + a₂x²+ a₃x³ + ···)(b₀ + b₁x + b₂x²+ b₃x³ + ···) =
 | <a₀b₀> +
 | <a₀·(b₁x + b₂x²+ b₃x³ + ···) + b₀·(a₁x + a₂x²+ a₃x³ + ···)> +
 | <0x + (a₁x + a₂x²+ a₃x³ + ···)(b₁x + b₂x²+ b₃x³ + ···)>
 | ==>
 | (cons-stream
 |   (* (stream-car as) (stream-car bs))
 |   (add-streams (add-streams (scale-stream (stream-cdr bs) (stream-car as))
 |                             (scale-stream (stream-cdr as)(stream-car bs)))
 |                (cons-stream 0 (mul-series (stream-cdr as) (stream-cdr bs))))
 |#

; mul-series procedure
(define (mul-series s1 s2)
  (cons-stream
    (* (stream-car s1) (stream-car s2))
    (add-streams (add-streams (scale-stream (stream-cdr s2) (stream-car s1))
                              (scale-stream (stream-cdr s1) (stream-car s2)))
                 (cons-stream 0 (mul-series (stream-cdr s1) (stream-cdr s2))))))
;Value: mul-series

1 ]=> ; cos²x + sin²x = 1
(stream-take
  (add-streams
    (mul-series cosine-series cosine-series)
    (mul-series sine-series sine-series))
  8)
;Value: (1 0 0 0 0 0 0 0)

1 ]=> 
End of input stream reached.
Ceterum censeo Carthaginem esse delendam.
