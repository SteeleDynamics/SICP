MIT/GNU Scheme running under GNU/Linux
Type `^C' (control-C) followed by `H' to obtain information about interrupts.

Copyright (C) 2020 Massachusetts Institute of Technology
This is free software; see the source for copying conditions. There is NO
warranty; not even for MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

Image saved on Sunday March 7, 2021 at 3:24:56 PM
  Release 11.2 || SF || LIAR/x86-64

1 ]=> ;
;  (define x (make-element key))
;
;      ┌─┬─┐  ┌─┬─┐                           ┌─┬───┬─┐
;  x ─>│╱│•┼─>│•│╱│         redrawn as    x ─>│╱│key│╱│
;      └─┴─┘  └┼┴─┘           ====>           └─┴───┴─┘
;              V
;            ┌───┐
;            │key│
;            └───┘
;
'()
;Value: ()

1 ]=> ; make-element constructor procedure
(define (make-element key)
  (cons '() (cons key '())))
;Value: make-element

1 ]=> ; prev-element selector procedure
(define (prev-element elt)
  (car elt))
;Value: prev-element

1 ]=> ; key-element selector procedure
(define (key-element elt)
  (cadr elt))
;Value: key-element

1 ]=> ; next-element selector procedure
(define (next-element elt)
  (cddr elt))
;Value: next-element

1 ]=> ; set-prev-element! mutator procedure
(define (set-prev-element! elt value)
  (set-car! elt value))
;Value: set-prev-element!

1 ]=> ; set-next-element! mutator procedure
(define (set-next-element! elt value)
  (set-cdr! (cdr elt) value))
;Value: set-next-element!

1 ]=> ;
; deque impl'ed as doubly-linked list of elemets with front and rear pointers
;
;
;      ┌─┬─┐
;  d ─>│•│•┼───────────────────────┐
;      └┼┴─┘                       │
;       │ front-ptr                │ rear-ptr
;       V                          V
;      ┌─┬─┬─┐  ┌─┬─┬─┐  ┌─┬─┬─┐  ┌─┬─┬─┐
;      │╱│w│•┼─>│•│x│•┼─>│•│y│•┼─>│•│z│╱│
;      └─┴─┴─┘  └┼┴─┴─┘  └┼┴─┴─┘  └┼┴─┴─┘
;           Λ    │   Λ    │   Λ    │
;           └────┘   └────┘   └────┘
;
;
'()
;Value: ()

1 ]=> ; make-deque constructor procedure
(define (make-deque)
  (cons '() '()))
;Value: make-deque

1 ]=> ; front-ptr selector procedure
(define (front-ptr deque)
  (car deque))
;Value: front-ptr

1 ]=> ; rear-ptr selector procedure
(define (rear-ptr deque)
  (cdr deque))
;Value: rear-ptr

1 ]=> ; set-front-ptr! mutator procedure
(define (set-front-ptr! deque elt)
  (set-car! deque elt))
;Value: set-front-ptr!

1 ]=> ; set-rear-ptr! mutator procedure
(define (set-rear-ptr! deque elt)
  (set-cdr! deque elt))
;Value: set-rear-ptr!

1 ]=> ; empty-deque? predicate procedure
(define (empty-deque? deque)
  (and (null? (front-ptr deque))
       (null? (rear-ptr deque))))
;Value: empty-deque?

1 ]=> ; front-deque selector procedure
(define (front-deque deque)
  (if (empty-deque? deque)
      (error "FRONT called with an empty deque" deque)
      (key-element (front-ptr deque))))
;Value: front-deque

1 ]=> ; rear-deque selector procedure
(define (rear-deque deque)
  (if (empty-deque? deque)
      (error "REAR called with an empty deque" deque)
      (key-element (rear-ptr deque))))
;Value: rear-deque

1 ]=> ; front-insert-deque! mutator procedure
(define (front-insert-deque! deque key)
  (let ((new-elt (make-element key)))
    (cond ((empty-deque? deque)
           (set-front-ptr! deque new-elt)
           (set-rear-ptr! deque new-elt)
           deque)
          (else
           (set-prev-element! (front-ptr deque) new-elt)
           (set-next-element! new-elt (front-ptr deque))
           (set-front-ptr! deque new-elt)
           deque))))
;Value: front-insert-deque!

1 ]=> ; rear-insert-deque! mutator procedure
(define (rear-insert-deque! deque key)
  (let ((new-elt (make-element key)))
    (cond ((empty-deque? deque)
           (set-front-ptr! deque new-elt)
           (set-rear-ptr! deque new-elt)
           deque)
          (else
           (set-next-element! (rear-ptr deque) new-elt)
           (set-prev-element! new-elt (rear-ptr deque))
           (set-rear-ptr! deque new-elt)
           deque))))
;Value: rear-insert-deque!

1 ]=> ; front-delete-deque! mutator procedure
(define (front-delete-deque! deque)
  (cond ((empty-deque? deque)
         (error "FRONT-DELETE! called with an empty deque" deque))
        ((null? (next-element (front-ptr deque)))
         (set-front-ptr! deque '())
         (set-rear-ptr! deque '())
         deque)
        (else
         (set-front-ptr! deque (next-element (front-ptr deque)))
         (set-prev-element! (front-ptr deque) '())
         deque)))
;Value: front-delete-deque!

1 ]=> ; rear-delete-deque! mutator procedure
(define (rear-delete-deque! deque)
  (cond ((empty-deque? deque)
         (error "REAR-DELETE! called with an empty deque" deque))
        ((null? (prev-element (rear-ptr deque)))
         (set-rear-ptr! deque '())
         (set-front-ptr! deque '())
         deque)
        (else
         (set-rear-ptr! deque (prev-element (rear-ptr deque)))
         (set-next-element! (rear-ptr deque) '())
         deque)))
;Value: rear-delete-deque!

1 ]=> ; print-deque procedure
(define (print-deque deque)
  (define (iter elt)
    (cond ((null? elt)
           (newline)
           'done)
          (else
           (display (key-element elt))
           (display " ")
           (iter (next-element elt)))))
  (newline)
  (iter (front-ptr deque)))
;Value: print-deque

1 ]=> ; unit-tests
(define d (make-deque))
;Value: d

1 ]=> (print-deque (front-insert-deque! d 'x))
x 
;Value: done

1 ]=> (print-deque (rear-insert-deque! d 'y))
x y 
;Value: done

1 ]=> (print-deque (front-insert-deque! d 'w))
w x y 
;Value: done

1 ]=> (print-deque (rear-insert-deque! d 'z))
w x y z 
;Value: done

1 ]=> (front-deque d)
;Value: w

1 ]=> (rear-deque d)
;Value: z

1 ]=> (print-deque (front-delete-deque! d))
x y z 
;Value: done

1 ]=> (print-deque (rear-delete-deque! d))
x y 
;Value: done

1 ]=> (print-deque (front-delete-deque! d))
y 
;Value: done

1 ]=> (print-deque (rear-delete-deque! d))

;Value: done

1 ]=> 
End of input stream reached.
..#]^@^@^@ NO CARRIER
