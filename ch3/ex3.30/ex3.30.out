MIT/GNU Scheme running under GNU/Linux
Type `^C' (control-C) followed by `H' to obtain information about interrupts.

Copyright (C) 2020 Massachusetts Institute of Technology
This is free software; see the source for copying conditions. There is NO
warranty; not even for MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

Image saved on Sunday March 7, 2021 at 3:24:56 PM
  Release 11.2 || SF || LIAR/x86-64

1 ]=> #|
 | wire mutable object procedures
 |#

; (helper) call-each procedure
(define (call-each procedures)
  (if (null? procedures)
      'done
      (begin
        ((car procedures))
        (call-each (cdr procedures)))))
;Value: call-each

1 ]=> ; make-wire constructor procedure
(define (make-wire)
  (let ((signal-value 0) (action-procedures '()))
    (define (set-my-signal! new-value)
      (if (not (= signal-value new-value))
          (begin (set! signal-value new-value)
                 (call-each action-procedures))
          'done))
    (define (accept-action-procedure! proc)
      (set! action-procedures (cons proc action-procedures))
      (proc))
    (define (dispatch m)
      (cond ((eq? m 'get-signal) signal-value)
            ((eq? m 'set-signal!) set-my-signal!)
            ((eq? m 'add-action!) accept-action-procedure!)
            (else (error "Unknown operation -- WIRE" m))))
    dispatch))
;Value: make-wire

1 ]=> ; get-signal selector procedure
(define (get-signal wire)
  (wire 'get-signal))
;Value: get-signal

1 ]=> ; set-signal! mutator procedure
(define (set-signal! wire new-value)
  ((wire 'set-signal!) new-value))
;Value: set-signal!

1 ]=> ; add-action! mutator procedure
(define (add-action! wire action-procedure)
  ((wire 'add-action!) action-procedure))
;Value: add-action!

1 ]=> ; probe procedure
(define (probe name wire)
  (add-action! wire
               (lambda ()
                 (newline)
                 (display name)
                 (display " ")
                 (display (current-time the-agenda))
                 (display "  New-value = ")
                 (display (get-signal wire)))))
;Value: probe

1 ]=> #|
 | queue mutable object procedures
 |#

; (helper) front-ptr selector procedure
(define (front-ptr queue) (car queue))
;Value: front-ptr

1 ]=> ; (helper) rear-ptr selector procedure
(define (rear-ptr queue) (cdr queue))
;Value: rear-ptr

1 ]=> ; (helper) set-front-ptr! mutator procedure
(define (set-front-ptr! queue item) (set-car! queue item))
;Value: set-front-ptr!

1 ]=> ; (helper) set-rear-ptr! mutator procedure
(define (set-rear-ptr! queue item) (set-cdr! queue item))
;Value: set-rear-ptr!

1 ]=> ; make-queue constructor procedure
(define (make-queue) (cons '() '()))
;Value: make-queue

1 ]=> ; empty-queue? predicate procedure
(define (empty-queue? queue) (null? (front-ptr queue)))
;Value: empty-queue?

1 ]=> ; front-queue selector procedure
(define (front-queue queue)
  (if (empty-queue? queue)
      (error "FRONT called with an empty queue" queue)
      (car (front-ptr queue))))
;Value: front-queue

1 ]=> ; insert-queue! mutator procedure
(define (insert-queue! queue item)
  (let ((new-pair (cons item '())))
    (cond ((empty-queue? queue)
           (set-front-ptr! queue new-pair)
           (set-rear-ptr! queue new-pair)
           queue)
          (else
           (set-cdr! (rear-ptr queue) new-pair)
           (set-rear-ptr! queue new-pair)
           queue))))
;Value: insert-queue!

1 ]=> ; delete-queue! mutator procedure
(define (delete-queue! queue)
  (cond ((empty-queue? queue)
         (error "DELETE! called with an empty queue" queue))
        (else
         (set-front-ptr! queue (cdr (front-ptr queue)))
         queue)))
;Value: delete-queue!

1 ]=> #|
 | agenda mutable object procedures
 | (a priority queue impl, not a binary heap)
 |#

; make-time-segment constructor procedure
(define (make-time-segment time queue)
  (cons time queue))
;Value: make-time-segment

1 ]=> ; segment-time selector procedure
(define (segment-time s) (car s))
;Value: segment-time

1 ]=> ; segment-queue selector procedure
(define (segment-queue s) (cdr s))
;Value: segment-queue

1 ]=> ; make-agenda constructor procedure
(define (make-agenda) (list 0))
;Value: make-agenda

1 ]=> ; current-time selector procedure
(define (current-time agenda) (car agenda))
;Value: current-time

1 ]=> ; set-current-time! mutator procedure
(define (set-current-time! agenda time)
  (set-car! agenda time))
;Value: set-current-time!

1 ]=> ; segments selector procedure
(define (segments agenda) (cdr agenda))
;Value: segments

1 ]=> ; set-segments! mutator procedure
(define (set-segments! agenda segments)
  (set-cdr! agenda segments))
;Value: set-segments!

1 ]=> ; first-segment selector procedure
(define (first-segment agenda) (car (segments agenda)))
;Value: first-segment

1 ]=> ; rest-segments selector procedure
(define (rest-segments agenda) (cdr (segments agenda)))
;Value: rest-segments

1 ]=> ; empty-agenda? predicate procedure
(define (empty-agenda? agenda)
  (null? (segments agenda)))
;Value: empty-agenda?

1 ]=> ; add-to-agenda! mutator procedure
(define (add-to-agenda! time action agenda)
  (define (belongs-before? segments)
    (or (null? segments)
        (< time (segment-time (car segments)))))
  (define (make-new-time-segment time action)
    (let ((q (make-queue)))
      (insert-queue! q action)
      (make-time-segment time q)))
  (define (add-to-segments! segments)
    (if (= (segment-time (car segments)) time)
        (insert-queue! (segment-queue (car segments))
                       action)
        (let ((rest (cdr segments)))
          (if (belongs-before? rest)
              (set-cdr!
               segments
               (cons (make-new-time-segment time action)
                     (cdr segments)))
              (add-to-segments! rest)))))
  (let ((segments (segments agenda)))
    (if (belongs-before? segments)
        (set-segments!
         agenda
         (cons (make-new-time-segment time action)
               segments))
        (add-to-segments! segments))))
;Value: add-to-agenda!

1 ]=> ; remove-first-agenda-item! mutator procedure
(define (remove-first-agenda-item! agenda)
  (let ((q (segment-queue (first-segment agenda))))
    (delete-queue! q)
    (if (empty-queue? q)
        (set-segments! agenda (rest-segments agenda)))))
;Value: remove-first-agenda-item!

1 ]=> ; first-agenda-item selector procedure
(define (first-agenda-item agenda)
  (if (empty-agenda? agenda)
      (error "Agenda is empty -- FIRST-AGENDA-ITEM")
      (let ((first-seg (first-segment agenda)))
        (set-current-time! agenda (segment-time first-seg))
        (front-queue (segment-queue first-seg)))))
;Value: first-agenda-item

1 ]=> #|
 | primitive function boxes (logic gates) procedures
 |#

; (helper) after-delay procedure
(define (after-delay delay action)
  (add-to-agenda! (+ delay (current-time the-agenda))
                  action
                  the-agenda))
;Value: after-delay

1 ]=> ; (helper) invalid-signal? predicate procedure
(define (invalid-signal? s)
  (not (or (= s 0)
           (= s 1))))
;Value: invalid-signal?

1 ]=> ; (helper) logical-not procedure
(define (logical-not s)
  (cond ((invalid-signal? s) (error "Invalid signal -- LOGICAL-NOT" s))
        ((= s 0) 1)
        ((= s 1) 0)))
;Value: logical-not

1 ]=> ; (helper) logical-and procedure
(define (logical-and s1 s2)
  (cond ((invalid-signal? s1) (error "Invalid signal s1 -- LOGICAL-AND" s1))
        ((invalid-signal? s2) (error "Invalid signal s2 -- LOGICAL-AND" s2))
        ((and (= s1 1) (= s2 1)) 1)
        (else 0)))
;Value: logical-and

1 ]=> ; (helper) logical-or procedure
(define (logical-or s1 s2)
  (cond ((invalid-signal? s1) (error "Invalid signal s1 -- LOGICAL-OR" s1))
        ((invalid-signal? s2) (error "Invalid signal s2 -- LOGICAL-OR" s2))
        ((or (= s1 1) (= s2 1)) 1)
        (else 0)))
;Value: logical-or

1 ]=> ; inverter constructor procedure
(define (inverter input output)
  (define (invert-input)
    (let ((new-value (logical-not (get-signal input))))
      (after-delay inverter-delay
                   (lambda ()
                     (set-signal! output new-value)))))
  (add-action! input invert-input)
  'ok)
;Value: inverter

1 ]=> ; and-gate constructor procedure
(define (and-gate a1 a2 output)
  (define (and-action-procedure)
    (let ((new-value
           (logical-and (get-signal a1) (get-signal a2))))
      (after-delay and-gate-delay
                   (lambda ()
                     (set-signal! output new-value)))))
  (add-action! a1 and-action-procedure)
  (add-action! a2 and-action-procedure)
  'ok)
;Value: and-gate

1 ]=> ; or-gate constructor procedure
(define (or-gate o1 o2 output)
  (define (or-action-procedure)
    (let ((new-value
           (logical-or (get-signal o1) (get-signal o2))))
      (after-delay or-gate-delay
                   (lambda ()
                     (set-signal! output new-value)))))
  (add-action! o1 or-action-procedure)
  (add-action! o2 or-action-procedure)
  'ok)
;Value: or-gate

1 ]=> #|
 | driver for digital circuits simulator
 |#

; propagate procedure
(define (propagate)
  (if (empty-agenda? the-agenda)
      'done
      (let ((first-item (first-agenda-item the-agenda)))
        (first-item)
        (remove-first-agenda-item! the-agenda)
        (propagate))))
;Value: propagate

1 ]=> #|
 | add'l digital circuits
 |#

;
;         half-adder
;      ┌──────────────────────────────────────────────────┐
;      │        ┌────────┐                                │
;  A ──┼─────•──┤        │         D          ┌────────┐  │
;      │     │  │or-gate ├────────────────────┤        │  │
;      │  ┌──┼──┤        │                 E  │and-gate├──┼── S
;      │  │  │  └────────┘                 ┌──┤        │  │
;      │  │  │                 ┌────────┐  │  └────────┘  │
;      │  │  │  ┌────────┐  ┌──┤inverter├──┘              │
;      │  │  └──┤        │  │  └────────┘                 │
;      │  │     │and-gate├──•─────────────────────────────┼── C
;  B ──┼──•─────┤        │                                │
;      │        └────────┘                                │
;      └──────────────────────────────────────────────────┘
;
; (define half-adder-delay
;   (max (+ or-gate-delay and-gate-delay)
;        (+ (* 2 and-gate-delay) inverter-delay)))
;

; half-adder procedure
(define (half-adder a b s c)
  (let ((d (make-wire)) (e (make-wire)))
    (or-gate a b d)
    (and-gate a b c)
    (inverter c e)
    (and-gate d e s)
    'ok))
;Value: half-adder

1 ]=> ;
;           full-adder
;        ┌──────────────────────────────────────────────────┐
;        │                    ┌────────┐                    │
;    A ──┼────────────────────┤ half-  ├────────────────────┼── SUM
;        │     ┌────────┐     │ adder  │     ┌────────┐     │
;    B ──┼─────┤ half-  ├─────┤        ├─────┤        │     │
;        │     │ adder  │     └────────┘     │or-gate ├─────┼── C-out
; C-in ──┼─────┤        ├────────────────────┤        │     │
;        │     └────────┘                    └────────┘     │
;        └──────────────────────────────────────────────────┘
;
; (define full-adder-delay
;   (+ (* 2 half-adder-delay)
;      or-gate-delay))
;

; full-adder procedure
(define (full-adder a b c-in sum c-out)
  (let ((s (make-wire))
        (c1 (make-wire))
        (c2 (make-wire)))
    (half-adder b c-in s c1)
    (half-adder a s sum c2)
    (or-gate c1 c2 c-out)
    'ok))
;Value: full-adder

1 ]=> ;
;           ripple-carry-adder
;        ┌─────────────────────────────────────────────┐
;        │     ┌────────┐                              │
;        │C0=0 ┤ full-  ├──────────────────────────────┼── S1
;   A1 ──┼─────┤ adder  │                              │
;   B1 ──┼─────┤        ├──┐                           │
;        │     └────────┘  │ C1                        │
;        │       ┌─────────┘                           │
;        │       │  ┌────────┐                         │
;        │       └──┤ full-  ├─────────────────────────┼── S2
;   A2 ──┼──────────┤ adder  │                         │
;   B2 ──┼──────────┤        ├──┐                      │
;        │          └────────┘  │ C2                   │
;        │                 ─────┘                      │
;                               .
;                               .
;                               .
;        │                           ┌─────            │
;        │                        Cn │  ┌────────┐     │
;        │                           └──┤ full-  ├─────┼── Sn
;   An ──┼──────────────────────────────┤ adder  │     │
;   Bn ──┼──────────────────────────────┤        ├─────┼── C
;        │                              └────────┘     │
;        └─────────────────────────────────────────────┘
;
;     (define ripple-carry-adder-delay
;       (* n full-adder-delay))
;
;     ripple-carry-adder-delay
; ==> (* n full-adder-delay)
; ==> (* n (+ (* 2 half-adder-delay) or-gate-delay))
; ==> (* n (+ (* 2 (max (+ or-gate-delay and-gate-delay)
;                       (+ (* 2 and-gate-delay) inverter-delay)))
;             or-gate-delay))
;

; ripple-carry-adder procedure
(define (ripple-carry-adder as bs ss c-out)
  (define (iter as bs ss c-in c-out)
    (cond ((not (null? as))
             (full-adder (car as) (car bs) c-in (car ss) c-out)
             (iter (cdr as) (cdr bs) (cdr ss) (make-wire) c-in))
          (else 'ok)))
  (iter as bs ss (make-wire) c-out))
;Value: ripple-carry-adder

1 ]=> #|
 | unit tests
 |#

; define the-agenda and delays for logic gates
(define the-agenda (make-agenda))
;Value: the-agenda

1 ]=> (define inverter-delay 2)
;Value: inverter-delay

1 ]=> (define and-gate-delay 3)
;Value: and-gate-delay

1 ]=> (define or-gate-delay 5)
;Value: or-gate-delay

1 ]=> ; define 4-bit numbers As, Bs, Ss; and carry-bit C
(define As (list (make-wire) (make-wire) (make-wire) (make-wire)))
;Value: as

1 ]=> (define Bs (list (make-wire) (make-wire) (make-wire) (make-wire)))
;Value: bs

1 ]=> (define Ss (list (make-wire) (make-wire) (make-wire) (make-wire)))
;Value: ss

1 ]=> (define C (make-wire))
;Value: c

1 ]=> ; wire up 4-bit ripple-carry-adder
(ripple-carry-adder As Bs Ss C)
;Value: ok

1 ]=> ; put probes on Ss
(probe 's1 (cadddr Ss))
s1 0  New-value = 0
;Unspecified return value

1 ]=> (probe 's2 (caddr Ss))
s2 0  New-value = 0
;Unspecified return value

1 ]=> (probe 's3 (cadr Ss))
s3 0  New-value = 0
;Unspecified return value

1 ]=> (probe 's4 (car Ss))
s4 0  New-value = 0
;Unspecified return value

1 ]=> (probe 'c C)
c 0  New-value = 0
;Unspecified return value

1 ]=> ; As ==> 6 decimal or 0110 binary
(set-signal! (caddr As) 1)
;Value: done

1 ]=> (set-signal! (cadr As) 1)
;Value: done

1 ]=> ; Bs ==> 7 decimal or 0111 binary
(set-signal! (cadddr Bs) 1)
;Value: done

1 ]=> (set-signal! (caddr Bs) 1)
;Value: done

1 ]=> (set-signal! (cadr Bs) 1)
;Value: done

1 ]=> ; run simulation ==> resulting in 1101 binary
(propagate)
s2 8  New-value = 1
s3 8  New-value = 1
s1 16  New-value = 1
s2 16  New-value = 0
s3 16  New-value = 0
s4 32  New-value = 1
s3 32  New-value = 1
;Value: done

1 ]=> 
End of input stream reached.
Post proelium, praemium.
